# Query 1 - Return all unique affiliations.
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
select distinct ?affiliation where { 
	?s 	aida35kschema:hasAffiliation ?affiliation .
}
# Query 2 - Return all distinct CSO topics.
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
select distinct ?topic where { 
	?s 	aida35kschema:hasCsoEnhancedTopic ?topic .
}

# Query 3 - Return TOP 10 Topics CSO with its count and name
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
select ?topic (count(?paper) as ?numberOfPapers) ?name where { 
    ?paper aida35kschema:hasJourName ?name .
	?paper 	aida35kschema:hasCsoEnhancedTopic ?topic .
} GROUP BY ?topic ?name ORDER BY DESC(?numberOfPapers) LIMIT 10 


# Query 4 - papers of the conference series ‘iswc’, count their references (citations), 
# and present them in decrease order of their count.
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
select (?s as ?paper) ?conf (count(?ref) as ?numberOfRefs) where { 
	?s 	aida35kschema:hasConfName ?conf .
    ?s aida35kschema:hasReference ?ref .
    filter contains(?conf,"iswc") .  
} GROUP BY ?s ?conf ORDER BY DESC(?NumberOfRefs)


# Query 5 - Find all authors that have an Affiliation with ULB, and present them along with their papers.
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
select ?author ?paper ?affiliation where {
    ?author aida35kschema:hasPaper ?paper .
    ?paper aida35kschema:hasAffiliationDistribution ?ad .
    ?ad aida35kschema:hasAffiliation ?affiliation .
    filter contains(?affiliation,"bruxelles") .
}

# Query 6 - get abstract for the paper
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
PREFIX ns4: <http://purl.org/dc/terms/>
select ?abstract where { 
    SERVICE <https://makg.org/sparql> { 
    <https://makg.org/entity/1642143707> ns4:abstract ?abstract } 
}

# Query 7 - Find all papers that have an Affiliation with ULB, 
# and for each one retrieve its abstract from the remote MAKG SPARQL endpoint
PREFIX aida35kschema: <http://aida.kmi.open.ac.uk/aida35k/ontology#>
PREFIX aida35k: <http://aida.kmi.open.ac.uk/aida35k/resource/>
PREFIX ns4: <http://purl.org/dc/terms/>

select ?paper ?abstract  where {
    ?author aida35kschema:hasPaper ?paper .
    ?paper aida35kschema:hasAffiliationDistribution ?ad .
    ?ad aida35kschema:hasAffiliation ?affiliation .
    filter contains(?affiliation,"bruxelles") .
    BIND(STRAFTER(str(?paper), "http://aida.kmi.open.ac.uk/aida35k/resource/p_") AS ?resource_id). 
    bind(IRI(concat("https://makg.org/entity/", ?resource_id)) as ?uri)
    SERVICE <https://makg.org/sparql> { 
    ?uri ns4:abstract ?abstract } 
}